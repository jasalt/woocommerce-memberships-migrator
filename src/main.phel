(ns woocommerce-memberships-migrator\main
  (:require phel\pdo :as pdo)
  (:require phel\str :as str)
  (:require woocommerce-memberships-migrator\lib\wp :as wp))

(defmacro read-config
  "Read PHP constant or throw exception if not set.
   Supports default values for optional configs."
  [name & [default]]
  (let [php-const-symbol (symbol (str "php/" name))]
    `(if (php/defined ,name)
       ,php-const-symbol
       (or ,default
           (php/new \RuntimeException (str "PHP constant " ,name " is required but not defined."))))))

(def db-prefix (read-config "SOURCE_MYSQL_DB_PREFIX"))

(def db-connection-string (str "mysql:host=" (read-config "SOURCE_MYSQL_HOST")
                               ";dbname="  (read-config "SOURCE_MYSQL_DB_NAME")
                               ";charset=" (read-config "SOURCE_MYSQL_DB_CHARSET")))

(def remote-db
  (let [conn (pdo/connect db-connection-string
                          (read-config "SOURCE_MYSQL_USER")
                          (read-config "SOURCE_MYSQL_PASSWORD"))]
    (pdo/exec conn (str "SET NAMES " (read-config "SOURCE_MYSQL_DB_CHARSET")
                        " COLLATE "  (read-config "SOURCE_MYSQL_DB_COLLATION")))
    conn))

(def sql-customer-users
  (str "SELECT
    u.ID,
    u.user_login,
    u.user_pass,
    u.user_nicename,
    u.user_email,
    u.user_registered,
    u.display_name,
    JSON_OBJECTAGG(um.meta_key, um.meta_value) AS user_meta
FROM " db-prefix "_users u
JOIN " db-prefix "_usermeta um ON u.ID = um.user_id
WHERE u.ID IN (
    SELECT user_id
    FROM " db-prefix "_usermeta
    WHERE meta_key = '" db-prefix "_capabilities'
    AND meta_value LIKE '%\"customer\";b:1;%'
)
GROUP BY u.ID, u.user_login, u.user_email, u.user_registered, u.display_name
ORDER BY u.ID;"))

(defn get-source-customers []
  (let [customers (wp/pdo-fetch-all remote-db sql-customer-users)]
    customers))

(def source-customers (get-source-customers))

(defn standardize-keys
  "When using custom database prefix, convert keys use standard `wp` prefix instead of it.
  E.g. `myprefix_capabilities` becomes `wp_capabilities`.
  If using standard prefix, nothing is done."
  [m]
  (if (= "wp" db-prefix)
    m
    (for [[k v] :pairs m :reduce [m {}]]
      (put m (if (str/starts-with? k db-prefix)
               (str/replace-first k db-prefix "wp")
               k) v))))

(defn keywordize-keys
  "Converts map keys to keywords.
  Recursively converts nested maps and arrays."
  [m]
  (cond
    (hash-map? m)
    (let [res (transient {})]
      (foreach [k v m]
        (put res (keyword k) (keywordize-keys v)))
      (persistent res))

    (indexed-php-array? m)
    (let [res (transient [])]
      (foreach [v m]
        (push res (keywordize-keys v)))
      (persistent res))

    true m))

(defn deserialize-values [m keys]
  "Deserialize predefined meta key values stored as serialized PHP"
  (for [key :in keys :reduce [acc m]]
    (put acc key (keywordize-keys (php->phel (php/unserialize (get m key)))))))

(defn replace-all
  "Does string replacements according to the `substitutions` map such as {\"Ã¶\" \"ö\" \"Ã¤\" \"ä\"}."
  [s substitutions]
  (for [[from to] :pairs substitutions :reduce [string s]]
    (str/replace string from to)))

(defn de-mojibake-values
  "Brute force fix dual encoding problems of source database strings.
  TODO action user configurable, not hardcoded"
  [m substitutions]
  (for [[k v] :pairs m :reduce [m {}]]
    (put m k (if (string? v)
               (replace-all v substitutions)
               v))))

(defn db-customer-to-map [customer]
  (let [meta  # process user_meta returned as JSON separately
        (-> (get customer :user_meta)
            (php/json_decode)
            (php/get_object_vars)
            (php->phel)
            (standardize-keys)
            (keywordize-keys)
            ## TODO keys and substitutions should be user selectable, not hardcoded
            (deserialize-values [:session_tokens :wp_capabilities :_wpcw_subs_expired_check])
            (de-mojibake-values {"Ã¶" "ö"
                                 "Ã¤" "ä"}))]
    (-> customer
        (de-mojibake-values {"Ã¶" "ö"
                             "Ã¤" "ä"})
        (put :user_meta meta))))

## Example db-customer-to-map return value
## {:ID 10 :user_login "foo.bar" :user_pass "\$P\$f00f0f0f0f0f000f0f0f0f00f0f" :user_nicename "foo-bar" :user_email "foo@example.com" :user_registered "2025-03-14 09:58:56" :display_name "Foo Bar" :user_meta {:wp_capabilities {:customer true} :wp_user_level "0" :description "" :last_update "1584178336" :_yoast_wpseo_profile_updated "1584177321" :icl_admin_language "fi" :billing_first_name "Foo" :billing_country "FI" :paying_customer "1" :_aw_user_registered "1" :nickname "foo.bar" :first_name "Foo" :session_tokens {:986da8s7d6f5a87s6d78987as6s67d89d8s76a6sd738417ff {:expiration 1585389210 :ip "123.234.345.456" :ua "Mozilla/5.0 (iPhone; CPU iPhone OS 13_3_1 like Mac OS X) AppleWebKit/605.1.15 (KHTML, like Gecko) Version/13.0.5 Mobile/15E148 Safari/604.1" :login 15841321234}} :shipping_method "" :billing_last_name "Bar" :last_name "Bar" :billing_email "foo@example.com" :itsec_user_activity_last_seen "15846654321" :admin_color "fresh" :icl_admin_language_migrated_to_wp47 "1" :show_admin_bar_front "true" :billing_city "Helsinki" :syntax_highlighting "true" :locale "fi" :use_ssl "0" :wp_nav_menu_recently_edited "2" :comment_shortcuts "false" :billing_address_1 "Poste Restante" :billing_phone "1234567890" :billing_postcode "00100" :rich_editing "true" :wc_last_active "1584654321" :_aw_persistent_language "fi" :_wpcw_subs_expired_check {:checked true :expires 1584704321} :itsec_user_activity_last_seen_notification_sent "1"}}

(comment
  (db-customer-to-map (first source-customers))

  (let [customer (first source-customers)]

    )


  (count source-customers)

  ## (php->phel (php/get_object_vars (php/json_decode (get customer :user_meta))))

  ## discard unneeded keys during user creation

  )


## TODO Add PHP associative array mapping of membership plans (old->new) in wp-config.php

## (def posts (php/get_posts
##             (to-php-array {"post_type" "post"
##                            "numberposts" 2})))
